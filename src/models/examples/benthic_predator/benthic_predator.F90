#include "fabm_driver.h"

!-----------------------------------------------------------------------
!BOP
!
! !MODULE: fabm_examples_benthic_predator --- test for benthic interfaces in FABM
!
! !INTERFACE:
   module fabm_examples_benthic_predator
!
! !DESCRIPTION:
! This is a very simple model for a benthic predator, grazing according to a
! Monod/Michaelis-Menten functional response on a pelagic prey, and
! respiring/dying according to a linear loss term. Variables for the prey
! (e.g., phytoplankon or zooplankton) and the target for the losses (typically
! a detrital or mineral pool) must be provided by an external model, e.g.,
! gotm\_npzd.
!
! !USES:
   use fabm_types
   use fabm_driver

   implicit none

!  default: all is private.
   private
!
! !PUBLIC MEMBER FUNCTIONS:
   public type_examples_benthic_predator, examples_benthic_predator_init, examples_benthic_predator_do_benthos
!
! !PUBLIC TYPES:
   type type_examples_benthic_predator
!     Variable identifiers
      type (type_bottom_state_variable_id) :: id_pred
      type (type_state_variable_id)        :: id_prey,id_nut

!     Model parameters: maximum grazing rate, half-saturation prey density, loss rate
      real(rk) :: g_max,K,h
   end type
!
! !REVISION HISTORY:!
!  Original author(s): Jorn Bruggeman
!
!EOP
!-----------------------------------------------------------------------

   contains

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Initialise the benthic predator model
!
! !INTERFACE:
   subroutine examples_benthic_predator_init(self,modelinfo,namlst)
!
! !DESCRIPTION:
!  Here, the examples\_benthic\_predator namelist is read and te variables
!  exported by the model are registered with FABM.
!
! !INPUT PARAMETERS:
   type (type_examples_benthic_predator),intent(out)   :: self
   _CLASS_ (type_model_info),            intent(inout) :: modelinfo
   integer,                              intent(in)    :: namlst
!
! !REVISION HISTORY:
!  Original author(s): Jorn Bruggeman
!
! !LOCAL VARIABLES:
   real(rk)                  :: pred_initial=0.01
   real(rk)                  :: g_max = 1., K=1., h=0.05
   character(len=64)         :: waste_target_variable='',prey_source_variable=''

   real(rk), parameter :: secs_pr_day = 86400.
   namelist /examples_benthic_predator/ waste_target_variable,prey_source_variable,pred_initial,g_max,K,h
!EOP
!-----------------------------------------------------------------------
!BOC
   ! Read the namelist
   read(namlst,nml=examples_benthic_predator,err=99,end=100)

   ! Store parameter values in our own derived type
   ! NB: all rates must be provided in values per day,
   ! and are converted here to values per second.
   self%g_max = g_max/secs_pr_day
   self%h     = h/secs_pr_day
   self%K     = K

   ! Register state variables
   ! NOTE the benthic=.true. argument, which specifies the variable is benthic.
   call register_state_variable(modelinfo,self%id_pred,'pred','mmol/m**2','predator density', &
                                          pred_initial,minimum=_ZERO_)

   ! Register link to external pelagic prey and mineral pools.
   ! Prey will be used to feed upon, mineral pool to place waste products in.
   call register_state_dependency(modelinfo,self%id_prey,prey_source_variable)
   call register_state_dependency(modelinfo,self%id_nut,waste_target_variable)

   return

99 call fatal_error('examples_benthic_predator_init','Error reading namelist examples_benthic_predator')
100 call fatal_error('examples_benthic_predator_init','Namelist examples_benthic_predator was not found')

   end subroutine examples_benthic_predator_init
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Right hand sides of benthic_predator model
!
! !INTERFACE:
   subroutine examples_benthic_predator_do_benthos(self,_ARGUMENTS_DO_BOTTOM_)
!
! !DESCRIPTION:
! This routine calculates the benthic sink and source terms, as well as
! (matching) bottom fluxes for pelagic variables. Both have units mmol/m**2/s.
!
! !INPUT PARAMETERS:
   type (type_examples_benthic_predator),       intent(in) :: self
   _DECLARE_ARGUMENTS_DO_BOTTOM_
!
! !REVISION HISTORY:
!  Original author(s): Jorn Bruggeman
!
! !LOCAL VARIABLES:
   real(rk)                   :: prey,pred,g
!EOP
!-----------------------------------------------------------------------
!BOC
   ! Enter spatial loops over the horizontal domain (if any).
   _HORIZONTAL_LOOP_BEGIN_

   ! Retrieve current (local) state variable values.
   _GET_(self%id_prey,prey)     ! prey density - pelagic
   _GET_HORIZONTAL_(self%id_pred,pred) ! predator density - benthic

   ! Calculate grazing rate
   g = self%g_max*pred*prey/(prey+self%K)

   ! Set local temporal derivatives of benthic variables
   _SET_ODE_BEN_(self%id_pred,g-self%h*pred)

   ! Set bottom fluxes of pelagic variables (these mirror local benthic derivatives)
   _SET_BOTTOM_EXCHANGE_(self%id_prey,-g)
   _SET_BOTTOM_EXCHANGE_(self%id_nut,self%h*pred)

   ! Leave spatial loops over the horizontal domain (if any).
   _HORIZONTAL_LOOP_END_

   end subroutine examples_benthic_predator_do_benthos
!EOC

!-----------------------------------------------------------------------

   end module fabm_examples_benthic_predator

!-----------------------------------------------------------------------
! Copyright by the GOTM-team under the GNU Public License - www.gnu.org
!-----------------------------------------------------------------------
